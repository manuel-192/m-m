#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=mouse-stuff
pkgdesc="Disable mouse wakeup"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=23
pkgrel=1

arch=(any)
license=(GPL)

source=(
    $url/$pkgname
    $url/$pkgname.service
    $url/$pkgname.timer
)
sha512sums=('f4e872a2d765cb39cd92bd7754871cc64863d100e76ec85081e157b62c8fc2bf2a557512b5bf62e4b13dbf92c919ed38d5dd866af52b732519303071a42b089c'
            '9e32697fcdb7f66d1d1b6e5f08c56329c548c79ece94cc812e5513d1f4c9dd897a4710d8050abd85c2929db192b3a71323490455b958c76dad9e70da100e0e78'
            '25a385090acbd14d035f6ba53c4bee4803a790bf73ad3823983a83dfe683ad9d95d901373239dd0f6f0a6ece9cbb03c88db87e8262feddf92ad632980a79db0c')

package() {
    local item src

    for src in "${source[@]}" ; do
        item="${src##*/}"
        case "$item" in
            *.completion)
                install -Dm644 "$item"   "$pkgdir/usr/share/bash-completion/completions/$(basename "$item" .completion)" ;;
            *.conf)
                install -Dm644 "$item"   "$pkgdir/etc/$item" ;;
            *.service | *.timer)
                install -Dm644 "$item"   "$pkgdir/etc/systemd/system/$item" ;;
            *)
                install -Dm755 "$item"   "$pkgdir/usr/bin/$item" ;;
        esac
    done
}
