#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=pachandle
pkgdesc="Helps merging files like .pacnew, .pacsave, and .pacorig"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=24
pkgrel=1.7

arch=('any')
license=('GPL')
depends=()

source=(
    $url/$pkgname
    $url/$pkgname.completion
)
sha512sums=('d37f1127d73d1148dabd96513d79d0d1ccabcdaeac0478a37e630643eea6b538f4ac9345c09d7feccfa33540ac80f8da204b3ebbd5e9b74d00924c01c8652689'
            'fde97e6095683f7787e74535f6a7ebb2811ec83a9bb25a4b2b971c43e1cb25f2433ff71e68faaa4e0453c53dcdb74f67811aa12fa2a301107376dda64701d321')

package() {
    local xx name
    for xx in "${source[@]}" ; do
        name="${xx##*/}"
        case "$name" in
            *.completion) install -Dm644 $name   $pkgdir/usr/share/bash-completion/completions/${name%.*} ;;
            *.conf)       install -Dm644 $name   $pkgdir/etc/$name ;;
            *)            install -Dm755 $name   $pkgdir/usr/bin/$name ;;
        esac
    done
}
