#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=local-repo-manager
pkgdesc="Manage local repo"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=23
pkgrel=16

arch=(any)
license=(GPL)
depends=(bash gnupg eos-bash-shared)
backup=(etc/$pkgname.conf)

source=(
    $url/$pkgname
    $url/$pkgname.conf
)
sha512sums=('28abe99a96bc9ca0f76eca55dae6dc7a6d78dc8138ebc8c17b9e3ba309b6d2c8ab0b1ce086af96c9d337c6d975814dd45c007abec50936200d72ac87a5f4bb1d'
            'edbe512ab11686e58bbb4c6dc27a598a7b5279a58a714fb9498f08d966e84292045a03982204a85c61dddb2475f24fa94295e4c636930a502b956a7d50439943')

package() {
    local item src

    for src in "${source[@]}" ; do
        item="${src##*/}"
        case "$item" in
            *.completion)
                install -Dm644 "$item"   "$pkgdir/usr/share/bash-completion/completions/$(basename "$item" .completion)" ;;
            *.conf)
                install -Dm644 "$item"   "$pkgdir/etc/$item" ;;
            *)
                install -Dm755 "$item"   "$pkgdir/usr/bin/$item" ;;
        esac
    done
}
