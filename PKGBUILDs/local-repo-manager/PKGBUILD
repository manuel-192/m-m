#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=(local-repo-manager)
pkgdesc="Manage local repo"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=24.9
pkgrel=1

arch=(any)
license=(GPL)
depends=(bash gnupg eos-bash-shared)
backup=(etc/$pkgname.conf)

source=(
    $url/$pkgname
    $url/$pkgname.bash-completion
    $url/$pkgname.conf
)
sha512sums=('e669bc9ceff57f67dc69997db448f36c7b4a2f8d658965ec63c4c44dc9cb9a4661f51737c9d806f1142ad20ac47ac24b2a4c6deb26f0643a3a07252df785797d'
            'c7499d052a1d68f1628160e997ce50b1d1585cb660946989beb317def3225127ed91d1b506c581df8c43374c04bd1f187fe4c4276fd9bae1136ed90884b3c792'
            'b654df18cc2a8976241018bd74cbf5a8755727e26b10652e7bef5d8085115f295bea191b58ad53ea904329f334d8231621b3f16ea0318cf51bbb15e387aa0d9c')

package() {
    local item src

    for src in "${source[@]}" ; do
        item="${src##*/}"
        case "$item" in
            *.completion | *.bash-completion)
                install -Dm644 "$item"   "$pkgdir/usr/share/bash-completion/completions/${item%.*}" ;;
            *.conf)
                install -Dm644 "$item"   "$pkgdir/etc/$item" ;;
            *)
                install -Dm755 "$item"   "$pkgdir/usr/bin/$item" ;;
        esac
    done
}
