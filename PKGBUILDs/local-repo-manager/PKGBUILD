#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=local-repo-manager
pkgdesc="Manage local repo"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=23
pkgrel=14

arch=(any)
license=(GPL)
depends=(bash gnupg eos-bash-shared)
backup=(etc/$pkgname.conf)

source=(
    $url/$pkgname
    $url/$pkgname.conf
)
sha512sums=('9c0ce491fc50d97bd5684c114a0de4c864207926edbe206bff79ac73f33e57ace660e45d1bd6669fbcc429b54bfd36970c1dbd130431022da731f82580fbba05'
            '7f8f4ce92f65d8771835fce44a8b3c51aede863a589e289403f746ceeb4bd1fec4aec419b95cd8d61d0f0b74f203d33361b56f66b5e4edb27e9a7922f75d04f0')

package() {
    local item src

    for src in "${source[@]}" ; do
        item="${src##*/}"
        case "$item" in
            *.completion)
                install -Dm644 "$item"   "$pkgdir/usr/share/bash-completion/completions/$(basename "$item" .completion)" ;;
            *.conf)
                install -Dm644 "$item"   "$pkgdir/etc/$item" ;;
            *)
                install -Dm755 "$item"   "$pkgdir/usr/bin/$item" ;;
        esac
    done
}
