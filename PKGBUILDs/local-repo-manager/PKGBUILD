#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=local-repo-manager
pkgdesc="Manage local repo"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=23
pkgrel=16.3

arch=(any)
license=(GPL)
depends=(bash gnupg eos-bash-shared)
backup=(etc/$pkgname.conf)

source=(
    $url/$pkgname
    $url/$pkgname.conf
)
sha512sums=('843446f1e3e46e1c6747f71cc7ebbfe5f35fdbba8aecbc4384436b136b77e944c9a8c62aaa4ce03d6e6f29d6b4ba9f1a006314c63c7ea4e3640da8b574d2b3a3'
            'edbe512ab11686e58bbb4c6dc27a598a7b5279a58a714fb9498f08d966e84292045a03982204a85c61dddb2475f24fa94295e4c636930a502b956a7d50439943')

package() {
    local item src

    for src in "${source[@]}" ; do
        item="${src##*/}"
        case "$item" in
            *.completion)
                install -Dm644 "$item"   "$pkgdir/usr/share/bash-completion/completions/$(basename "$item" .completion)" ;;
            *.conf)
                install -Dm644 "$item"   "$pkgdir/etc/$item" ;;
            *)
                install -Dm755 "$item"   "$pkgdir/usr/bin/$item" ;;
        esac
    done
}
