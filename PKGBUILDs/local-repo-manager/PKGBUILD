#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=(local-repo-manager)
pkgdesc="Manage local repo"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=25
pkgrel=1

arch=(any)
license=(GPL)
depends=(bash gnupg eos-bash-shared)
backup=(etc/$pkgname.conf)

source=(
    $url/$pkgname
    $url/$pkgname.bash-completion
    $url/$pkgname.conf
)
sha512sums=('5b526d9de5e0b55a6d04643117a6a266588f9a34ffc41a44f9605fcf372ef25e187b5ce617108a762941fd9d52f65fec41cf99a5499dc86d529f3c33662f9ce8'
            'c7499d052a1d68f1628160e997ce50b1d1585cb660946989beb317def3225127ed91d1b506c581df8c43374c04bd1f187fe4c4276fd9bae1136ed90884b3c792'
            'b654df18cc2a8976241018bd74cbf5a8755727e26b10652e7bef5d8085115f295bea191b58ad53ea904329f334d8231621b3f16ea0318cf51bbb15e387aa0d9c')

package() {
    local item src

    for src in "${source[@]}" ; do
        item="${src##*/}"
        case "$item" in
            *.completion | *.bash-completion)
                install -Dm644 "$item"   "$pkgdir/usr/share/bash-completion/completions/${item%.*}" ;;
            *.conf)
                install -Dm644 "$item"   "$pkgdir/etc/$item" ;;
            *)
                install -Dm755 "$item"   "$pkgdir/usr/bin/$item" ;;
        esac
    done
}
