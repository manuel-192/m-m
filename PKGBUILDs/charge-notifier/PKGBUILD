#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=charge-notifier
pkgdesc="Notify when battery is low or high"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=24
pkgrel=8.4

arch=('any')
license=('GPL')
depends=()
backup=()

source=(
    $url/battnot
    $url/battnot.completion
)
sha512sums=('b918147fcc6831aa9aaf0209c8e85421ad4c497abcf17d0ada7618466ec85c370f98df7f7e553b35ba71fdaf28763ac50d126ebc368ed4dbb2e125887b7bba88'
            '220ec781df12490a2e9516a7544abaad86b30b01f29fa440f01633ad2c92ce85ad1fb290d84d734f2d4823a1c17381918d1f22785a24d11082e6a14a67a06ffd')

package() {
    local xx name
    for xx in "${source[@]}" ; do
        name=${xx##*/}
        case "$name" in
            *.completion) install -Dm644 $name   $pkgdir/usr/share/bash-completion/completions/${name%.*} ;;
            *.conf)       install -Dm644 $name   $pkgdir/etc/$name ;;
            *)            install -Dm755 $name   $pkgdir/usr/bin/$name ;;
        esac
    done
}
