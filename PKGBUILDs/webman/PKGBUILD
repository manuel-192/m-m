#!/bin/bash
# Maintainer: manuel (see forum.endeavouros.com)

pkgname=webman
pkgdesc="Similar to 'man', but gets man pages from the Arch web. Also can find other info if man page doesn't exist."
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=2021.7.31
pkgrel=1

arch=('any')
license=('GPL')
depends=('bash')

_webmandir=etc/$pkgname
backup=($_webmandir/$pkgname.conf)

source=(
    "$url/$pkgname"
    "$url/$pkgname.conf"
    "$url/$pkgname.completion"
    "$url/${pkgname}-pageinfos.tar.gz"
)
sha512sums=('e50086ea02572b9d26a732d97b969070b25d9caeb9900c458e7e30d5391567d3f63971e68871859f03153dd1b077206d2fd2f4bd077ec7baa32338ba9b847769'
            '1bbc39e6708c2c8120c4fe473c5880da664f10dcce9842e069ca52a14037b63c3f1c93539a2de69be0e564f74530b46ee445ff409b2108b67c19f7cef41470b2'
            '3ff932ce73a92dfd72226f414b6d220ebb0e8b953c5ed2cee2ebbe9152947505f4b274b7224278dce8a72d8a2ee76f11c9de886b1165c8e82f871c5a34e007c2'
            '43f4bdad3622249eeb39f29d8ada8a97b322e688f6e4e055298c68019164a258e520338f12a2a32886d2ee5bb27b9d501d90e45f93c736ef5792f4bff0d6b8a2')

package() {
    local xx

    install -Dm755 $pkgname             $pkgdir/usr/bin/$pkgname
    install -Dm644 $pkgname.completion  $pkgdir/usr/share/bash-completion/completions/$pkgname
    install -Dm644 $pkgname.conf        $pkgdir/$_webmandir/$pkgname.conf

    install -d $pkgdir/$_webmandir
    for xx in pageinfo/* ; do
        case "$xx" in
            *.helpcmd)
                install -Dm755 "$xx" "$pkgdir/$_webmandir/$xx" ;;
            *.url | *)
                install -Dm644 "$xx" "$pkgdir/$_webmandir/$xx" ;;
        esac
    done
}
