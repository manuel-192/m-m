#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=wallpaper-changer
pkgdesc="Change the wallpaper periodically"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=24.10.12
pkgrel=1

arch=('any')
license=('GPL')
backup=(etc/$pkgname.conf)
depends=(eos-bash-shared fd)

source=(
    $url/$pkgname
    $url/$pkgname.completion
    $url/$pkgname.conf
)
sha512sums=('61da7036c159715442d568b0506967bf7793358d8ba137d819d0ebae36f5eb90eab407e89693721d36a40294e82f13aece6f81ca17bc76d8fcf260d242ae5a59'
            '5639fb432d253d564f4b2109d846610ec3f6f491cd4f1bfd9168e25774ca50331f2d66f415378eb26d67940d4f7b66f1778a1b9ecb0a32dbb1b839727d436643'
            '29c919d72cb75730e15b7f39c9c6d4e44d4e2595dd60c850844034eaaaf58a3e57b2ab449b3d307f72b26b53c564a42940bc495c249f05f301b73a59272e0a51')

package() {
    local file urlpath

    for urlpath in "${source[@]}" ; do
        file=${urlpath##*/}
        case "$file" in
            *.completion)           install -Dm644 $file   "$pkgdir/usr/share/bash-completion/completions/${file%.completion}" ;;
            *.conf)                 install -Dm644 $file   "$pkgdir/etc/$file" ;;
            *)                      install -Dm755 $file   "$pkgdir/usr/bin/$file" ;;
        esac
    done
}
