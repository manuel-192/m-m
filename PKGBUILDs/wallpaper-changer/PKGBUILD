#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=wallpaper-changer
pkgdesc="Change the wallpaper periodically"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=24.12.1
pkgrel=1

arch=('any')
license=('GPL')
backup=(etc/$pkgname.conf)
depends=(eos-bash-shared fd)

source=(
    $url/$pkgname
    $url/$pkgname.completion
    $url/$pkgname.conf
)
sha512sums=('85e2b8ad75e5a2d1db89a873dff2c6d562bb254d77d192296d386c31193488abbd67ee8511945115bf19ecd2a5c4e0cb31b3b17a7cb06479aab4704ed76c9a33'
            '5639fb432d253d564f4b2109d846610ec3f6f491cd4f1bfd9168e25774ca50331f2d66f415378eb26d67940d4f7b66f1778a1b9ecb0a32dbb1b839727d436643'
            '29c919d72cb75730e15b7f39c9c6d4e44d4e2595dd60c850844034eaaaf58a3e57b2ab449b3d307f72b26b53c564a42940bc495c249f05f301b73a59272e0a51')

package() {
    local file urlpath

    for urlpath in "${source[@]}" ; do
        file=${urlpath##*/}
        case "$file" in
            *.completion)           install -Dm644 $file   "$pkgdir/usr/share/bash-completion/completions/${file%.completion}" ;;
            *.conf)                 install -Dm644 $file   "$pkgdir/etc/$file" ;;
            *)                      install -Dm755 $file   "$pkgdir/usr/bin/$file" ;;
        esac
    done
}
