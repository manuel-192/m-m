#!/bin/bash

# Maintainer: manuel (see forum.endeavouros.com)

pkgname=meld-rcs
pkgdesc="Meld with RCS support and small UI changes"
url="https://github.com/manuel-192/m-m/raw/master/PKGBUILDs/$pkgname"
pkgver=25
pkgrel=1

arch=('any')
license=('GPL')

source=(
    $url/meld               # overrides /bin/meld
    $url/meldrcs
    $url/meldgit
)
sha512sums=('8d5d66286c5c234b3e322a2d05781fd52550da0efe39a6beea9679950c98a4d91dbc9d084d90fc4b3e44434a29f5c0b3834867c443c53eed578d7eed05751372'
            '7392aba60c2c8cf0838fbf1b70f57caa8dede34316e71905c556d7571818d3bb5ba6c2ba1e43440188ea5d810ef1d90e601407408d6ddab89faf6554a40cc181'
            '8550f621f03be4a622273aa7ab849f6dcb2c365f072ac325f88b50299efca9382f58e1f16c478682b41877a5b5c7d005d15c131d82c96be5151ca9957d0d45c2')

package() {
    local src file

    for src in "${source[@]}" ; do
        file="${src##*/}"
        case "$file" in
            *.conf)   install -Dm644 $file    $pkgdir/etc/$file ;;
            meld)     install -Dm755 $file    $pkgdir/usr/local/bin/$file ;;
            *)        install -Dm755 $file    $pkgdir/usr/bin/$file ;;
        esac
    done
}
